plugins {
    id 'java'
    id 'maven'

    id 'net.minecraftforge.gradle.forge' version '8708bf3e0'
    id 'org.spongepowered.mixin' version '0.6-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

version = "1.0.1"
group = "co.uk.isxander"
archivesBaseName = "xanderlib"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"

    makeObfSourceJar = false

    replace '@version@', version
}

mixin {
    defaultObfuscationEnv = "notch"
    add sourceSets.main, "mixins.xanderlib.refmap.json"
}

repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name = 'sk1er'
        url = 'https://repo.sk1er.club/repository/maven-public'
    }
}
configurations {
    include
    implementation.extendsFrom(include)
}


dependencies {
    provided files("libs/modcore.jar")
    
    // Languages
    include 'org.scala-lang:scala-library:2.11.12'

    // Utilities
    include('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
    annotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"

    include 'com.squareup.okhttp3:okhttp:4.9.1'
    include 'org.javassist:javassist:3.27.0-GA'
    include 'org.reflections:reflections:0.9.12'
}

processResources{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
       
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    archiveName = "${project.version}.jar"

    manifest.attributes(
            "MixinConfigs": 'mixins.xanderlib.json',
            "TweakClass": 'co.uk.isxander.xanderlib.mixins.XanderLibTweaker',
            "TweakOrder": 0,
            "Manifest-Version": 1.0
    )
}

shadowJar {
    archiveBaseName.set("${project.version}")
    archiveVersion.set('')
    archiveClassifier.set('')

    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    // https://youtrack.jetbrains.com/issue/KT-25709
    exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_module'
    exclude '**/*.kotlin_builtins'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

reobfJar.dependsOn tasks.shadowJar
